;*****************************************************************************
; AutoHotkey Version:  1.0.41.01
; Автор:               Androgen
; Имя скрипта:         Переключить язык по умолчанию.ahk (v.1.3)
;*****************************************************************************
; Проверить и, если нужно - переключить, язык ввода по умолчанию (тот, что при загрузке Windows).
;*****************************************************************************
 
; ========== НАСТРОЙКИ ПОЛЬЗОВАТЕЛЯ ==========
Name_Lang_Serv_Win = Языки и службы текстового ввода
; как у вас называется это окно (я встречал и другие названия)
; ========== КОНЕЦ НАСТРОЕК ПОЛЬЗОВАТЕЛЯ ==========
 
#NoTrayIcon ; не показывать иконку скрипта в трее
RegRead, Load_Lang, HKEY_CURRENT_USER, Keyboard Layout\Preload, 1
; прочитать, какая раскладка по умолчанию
if Load_Lang = 00000419 ; если русская, то...
{
        MsgBox, 1, Внимание., Текущий язык ввода по умолчанию - русский.`n`nИзменить на АНГЛИЙСКИЙ ?
        ; оформляем сообщение
    IfMsgBox, OK ; если "OK", то...
        Key_Action = {Up} ; эта клавиша будет высылаться контролу
        ; для смены раскладки на английскую (если нужно - поправьте)
    else ; если кнопка подтверждения не нажата, то...
        Exit ; завершить скрипт
}
if Load_Lang = 00000409 ; если английская, то...
{
        MsgBox, 1, Внимание., Текущий язык ввода по умолчанию - английский.`n`nИзменить на РУССКИЙ ?
        ; оформляем сообщение
    IfMsgBox, OK ; если "OK", то...
        Key_Action = {Down} ; эта клавиша будет высылаться контролу
        ; для смены раскладки на русскую (если нужно - поправьте)
    else ; если кнопка подтверждения не нажата, то...
        Exit ; завершить скрипт
}
if (Load_Lang <> 00000419 AND Load_Lang <> 00000409)
; если раскладки по умолчанию не русская и не английская, то...
{
        MsgBox, 0, Внимание!, %A_Space%Текущий язык НЕ АНГЛИЙСКИЙ и НЕ РУССКИЙ.`n`n
        , Скрипт рассчитан на работу с этими раскладками.`n`n
        , Измените настройки скрипта. ; оформляем сообщение
        Exit ; завершить скрипт
}
 
Run, RunDll32.exe shell32.dll`,Control_RunDLL input.dll
; запустить апплет "Языки и службы текстового ввода"
WinWait, %Name_Lang_Serv_Win%,, 5
; подождать окно "Языки и службы текстового ввода" не более 5 секунд
If ErrorLevel <> 0 ; если окна так и не дождались, то...
{
    MsgBox, Не дождались окна: "%Name_Lang_Serv_Win%"`n`n
    , Проверьте в скрипте "НАСТРОЙКИ ПОЛЬЗОВАТЕЛЯ" ; оформляем сообщение
        Exit ; завершить скрипт
}
; если окно появилось, то...
ControlSend, ComboBox1, %Key_Action%, %Name_Lang_Serv_Win% ; выбрать язык
Sleep, 100 ; пауза для надежности переключения раскладки
;~ pause ; для проверки переключения раскладки, раскомментируйте эту строку,
; а директиву #NoTrayIcon (в начале скрипта) - закомментируйте
metka0: ; метка для повторного клика, если понадобится
ControlClick, Button10 ; кликнуть "OK"
if ErrorLevel <> 0 ; если контрол не был готов (а такое случается), то...
    goto, metka0 ; повторить клик
Exit ; завершить скрипт